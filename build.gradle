group 'poad.functional.type'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: "maven"
apply plugin: "signing"

archivesBaseName = "tuple4j"

description = "Tuple for Java"


sourceCompatibility = 1.8


//set build variables based on build type (release, continuous integration, development)
def isDevBuild
def isCiBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
    version += "-SNAPSHOT"
}

repositories {
    mavenCentral()
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.tngtech.jgiven:jgiven-junit:0.15.1'
    testCompile 'org.assertj:assertj-core:3.7.0'
    testCompile 'org.scala-lang:scala-library:2.12.2'
}

compileJava {
    gradle.projectsEvaluated {
        if (project.hasProperty('warn')) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}


uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
        else {
            mavenDeployer {
                if(isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'tuple4j'
                    packaging 'jar'
                    description 'Tuple for Java'
                    url 'https://github.com/poad/tuple4j'

                    scm {
                        url "scm:git@github.com:isucon/isucon-bench-java.git"
                        connection "scm:git@github.com:poad/tuple4j.git"
                        developerConnection "scm:git@github.com:poad/tuple4j.git"
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id 'poad'
                            name 'Kenji Saito'
                            email 'poad1010@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
